# 3.メモリ管理ミスがある不完全な一般化

[](## 3.Unsound generalization as memory mismanagement)

[](This section begins to introduce the ideas behind Rémys algorithm, relating them to region-based memory management.)
このセクションでは、領域ベースのメモリ管理にそれらを関連して、レミのアルゴリズムの背後にあるアイデアを導入し始めます。
[](For concreteness we will be using a toy Hindley-Milner type inferencer.)
具体性のために我々はおもちゃのHindley-Milner型推論を使用することにします。
[](In this section, the inferencer has the unsound generalization function that quantifies free type variables in a type with no regard for the environment.)
このセクションでの、型推論器は環境とは関係ない型に自由型変数を量化する不完全な一般化機能を持ちます。
[](We type check in detail three trivial examples, and relate the inferring of unsound types with the common problems of the manual memory management, releasing memory still in use.)
我々は詳細に3つの簡単な例で確認し、まだ使用中のメモリを解放する手動のメモリ管理の一般的な問題との不完全な型推論を関連付けます。
[](The unsound generalization will be fixed in the next section, drawing the inspiration from the standard methods of preventing premature deallocation of resources.)
不完全な一般化は、次のセクションで修正しますが、リソースの早期割り当て解除を防止する標準的な方法からインスピレーションを引き出します。

[](Although our Hindley-Milner type inferencer is toy, it shares many implementation decisions [and even some function names] with the real OCaml type checker. Understanding it will help when we turn to OCaml internals later on this page.)
我々のHindley-Milner型推論はおもちゃとはいえ、実際のOCamlの型検査と多くの実装方法（さらにいくつかの関数名）を共有しています。後でこのページはOCamlの内部の理解に取りかかったときに役立つでしょう。

[](Our toy language is the standard pure lambda-calculus with let.)
我々のtoy言語は、letのある標準的な純粋なλ計算です。
[](Its expressions are)
その式は以下のようになります。

```
     type exp =
       | Var of varname                      (* 変数 *)
       | App of exp * exp                    (* 適用: e1 e2      *)
       | Lam of varname * exp                (* 抽象: fun x -> e *)
       | Let of varname * exp * exp          (* let x = e in e2 *)
```

[](Types are comprised of type variables, quantified type variables and function types:)
型は型変数、量化された型変数と関数の型で構成されています:

```
     type qname = string
     type typ =
       | TVar of tv ref               (* 型 (schematic) 変数 *)
       | QVar of qname                (* 量化型変数 *)
       | TArrow of typ * typ
     and tv = Unbound of string | Link of typ
```

[](Type schemas in the Hindley-Milner system are in the prenex form [that is, universal quantifiers are all outside], and so the quantifiers need not be represented explicitly.)
Hindley-Milnerシステム内の型スキーマは（つまり、全称量化子は、すべて外にある）冠頭形であり、など量化を明示的に表現する必要はありません。
[](Types with QVar are type schemas; without -- simple types.)
単純型のない`QVar`を持つ型は、型のスキーマです。

[](In the Prolog tradition, type variables are represented as reference cells.)
Prologの伝統では、型変数は参照セルとして表されます。
[](An unbound variable contains the null or the self pointer -- or, in our case, the name of the variable for easy printing.)
バインドされていない変数はNULLまたは自己のポインターが含まれています - あるいは、我々の場合、簡単にプリントできるの変数の名前が含まれています。
[](When a free type variable is unified with some type t', the reference cell is overwritten with the pointer to t'.)
自由型変数が何らかの型`t'`に単一化されたときに、参照セルは`t'`へのポインタで上書きされます。
[](To prevent cyclical [and, for us, unsound] types, the 'occurs check' is performed first: occurs tv t' traverses t' raising an exception if it comes across the type variable tv:)
型が循環する事（そして、我々の不完全さ）を防ぐために、 '出現チェック' (occurs check) が最初に実行されます: `tv t'`を含むトラバース`t'`は、型変数`tv`に遭遇した場合に例外を発生します:

```
     let rec unify : typ -> typ -> unit = fun t1 t2 ->
       if t1 == t2 then ()                   (* t1 and t2 are physically the same *)
                                             (* t1とt2 が物理的に同じ *)
       else match (t1,t2) with
       | (TVar ({contents = Unbound _} as tv),t') | (t',TVar ({contents = Unbound _} as tv)) ->
           occurs tv t'; tv := Link t'
       | (TVar {contents = Link t1},t2) | (t1,TVar {contents = Link t2}) ->
           unify t1 t2
       | (TArrow (tyl1,tyl2), TArrow (tyr1,tyr2)) ->
           unify tyl1 tyr1;
           unify tyl2 tyr2
       (* everything else is error *)
       (* 他はすべてエラー *)
```

[](The type checker is totally standard.)
型チェッカーはまったくもって標準的な作りです。
[](It infers the type for the expression exp in the type environment env:)
exp型の式をenv型の環境中で推論します：

```
     type env = (varname * typ) list
     let rec typeof : env -> exp -> typ = fun env -> function
       | Var x     -> inst (List.assoc x env)
       | Lam (x,e) ->
           let ty_x = newvar () in
           let ty_e = typeof ((x,ty_x)::env) e in
           TArrow(ty_x,ty_e)
       | App (e1,e2) ->
           let ty_fun = typeof env e1 in
           let ty_arg = typeof env e2 in
           let ty_res = newvar () in
           unify ty_fun (TArrow (ty_arg,ty_res));
           ty_res
       | Let (x,e,e2) ->
           let ty_e = typeof env e in
           typeof ((x,gen ty_e)::env) e2
```

[](The function newvar allocates a new TVar, with a unique name. )
newvar関数は一意の名前で、新しいTVarを割り当てます。
[](The function inst instantiates a type schema, that is, replaces each QVar with a fresh TVar.)
関数instはタイプスキーマをインスタンス化する、すなわち、それぞれのQVarを新鮮なTVarに置き換えます。
[](It is also standard.)
これもまた標準的で普通のやりかたです。
[](The generalization function is unsound: it quantifies all free variables in the type regardless of the environment:)
一般化関数は未完成です: 環境内の型に関係なくすべての自由変数を量化してしまいます:

```
let rec gen : typ -> typ = function      (* 未完成! *)
  | TVar {contents = Unbound name} -> QVar name
  | TVar {contents = Link ty}      -> gen ty
  | TArrow (ty1,ty2)               -> TArrow (gen ty1, gen ty2)
  | ty -> ty
```

[](The quantification replaces a TVar with the corresponding QVar.)
量化とは、TVarを対応するQVarで置き換える事です。
[](The original TVar is hence implicitly deallocated: When a free variable is bound, it 'disappears', being replaced by the 'pointer' to the binder.)
元のTVarは以下の理由で暗黙的に割り当てを解除されています: 自由変数がバインドされると、「消す」ために、バインダーに対する「ポインタ」で置き換えられます。

[](With respect to type variables, typeof allocates free variables, unifies them, and deallocates, after quantification.)
変数の型に関して、typeofは量化した後に、自由変数を割り当て、それらを単一化して、割り当てを解除します。

----------

[](Let us type check simple examples observing the sequence of these three main operations that affect free type variables. )
自由型変数に影響を与える、これらの3つの主要な一連の動作を観察する簡単な例を確認してみましょう。
[](The first example is the one where nothing should go wrong:)
最初の例は、何も間違いはないでしょう:

```
fun x -> let y = fun z -> z in y
```

[](The trace of type-checking, showing only type-variable related operations, is as follows.)
次のように型チェックのトレースは、一つの型変数に関連する動作を示します。

```
1    ty_x = newvar ()          (* fun x -> ...               *)
2      ty_e =                  (* let y = fun z -> z in y    *)
3         ty_z = newvar ();    (* fun z -> ...               *)
3         TArrow(ty_z,ty_z)    (* 以下に対して推論: fun z -> z  *)
2      ty_y = gen ty_e         (* ty_z がfreeなまま等          *)
2      deallocate ty_z         (* 量化し消す                 *)
1    TArrow(ty_x, inst ty_y)   (* 以下に対して推論: fun x -> ...*)
```

[](The number in the first column is the depth for the recursive invocations of typeof.)
最初の列の数は、typeofの再帰呼び出しの深さです。
[](Since typeof recurs on each non-leaf node of the abstract syntax tree [AST], this recursive invocation depth is the depth in the AST of the node being type checked. )
typeofは抽象構文木（AST）の各非リーフノード上で繰り返されるので、この再帰呼び出しの深さが型チェックされているノードのASTにおける深さです。

[](The inferred type is 'a -> 'b -> 'b, as expected.)
推論された型は、期待どおり `'a -> 'b -> 'b` です。
[](Nothing went wrong.)
間違いありません。

----------

[](The second example is the one we saw before, for which the unsound generalization gives the unsound type 'a->'b:)
第二の例は、`'a->'b` は不完全な一般化は不完全な型を取得するために、前に示したものです：

```
fun x -> let y = x in y
```

[](Diagramming the TVar operations as before reveals the problem:)
TVar操作の図は以前のように、問題を明らかにしています:

```
1    ty_x = newvar ()          (* fun x -> ...                 *)
2      ty_e =                  (* let y = x in y               *)
3         inst ty_x            (* xについて推測し、ty_xと同じ   　 *)
2      ty_y = gen ty_e         (* ty_xは、freeのままで、ある      *)
2      deallocate ty_x         (* 量化、及び削除                *)
1    TArrow(ty_x, inst ty_y)   (* 以下の推論: fun x -> ...       *)
```

[](The type variable ty_x is part of the return type, used at depth 1 -- and yet it is quantified and disposed of at depth 2.)
型変数ty_xは深さ1で使用し、戻り値の型の一部であり-そして、まだそれは、深さ2で定量し、解放されます。
[](We disposed of the value still in use.)
我々は、まだ使用中の値を解放しました。

----------

[](The third example is also problematic.)
第三の例にも問題があります。
[](The unsound generalization again gives the unsound type ['a->'b] -> ['c ->'d]:)
不完全な一般化は再び不完全な型 `('a->'b) -> ('c->'d)`を与えます：

```
     fun x -> let y = fun z -> x z in y
```

[](The diagram shows a memory management problem again:)
図は、再びメモリ管理の問題を示しています:

```
     1    ty_x = newvar ()          (* fun x -> ...               *)
     2      ty_e =                  (* let y = ...                *)
     3         ty_z = newvar ()     (* fun z -> ...               *)
     4           ty_res = newvar () (* 型チェック: x z             *)
     4           ty_x :=            (* 単一化の結果                *)
     4              TArrow (ty_z,ty_res)
     4           ty_res             (* x z を推論                *)
     3         TArrow(ty_z,ty_res)  (* fun z -> x z を推論       *)
     2      ty_y = gen ty_e         (* ty_z, ty_res は自由のまま   *)
     2      deallocate ty_z         (* 量化され解放される         *)
     2      deallocate ty_res       (* 量化され解放される         *)
     1    TArrow(ty_x, inst ty_y)   (* fun x -> ... を推論       *)
```

[](The type variables ty_z and ty_res are quantified over and hence disposed of at depth 2, and yet they are part of TArrow (ty_z,ty_res) that is assigned to ty_x, which, in turn, is part of the result.)
型変数`ty_z`と`ty_res`は量化が終了し、したがって、深さ2で解放され、そしてまだ彼らは、`ty_x`に割り当てられ`TArrow(ty_z,ty_res)`の一部であり、今度は、結果の一部です。

----------

[](All unsound examples had a 'memory management problem', deallocating memory [TVar] still being used.)
すべての不完全な例は、メモリ管理の問題を持っていて、割り当て解除されたメモリ(`TVar`)はまだ使用されています。
[](This is no accident.)
これは偶然ではありません。
[](When a type variable is quantified over, later on it can be instantiated with any type whatsoever, with no limitations and in any context.)
型変数が上の量化された場合、この後、制限なしで、どの文脈のいかなる型でもインスタンス化することができます。
[](However, if a type variable also appears in the type environment G, no longer can we replace the type variable with any type without affecting the rest of the type checking.)
しかし、型変数がまた型環境Gに現れた場合は、もはや我々は型チェックの残りの部分に影響を与えることなく、任意の型で型変数を置き換えることができません。
[](Likewise, when we free a piece of memory, we give the run-time the permission to reallocate it and overwrite with arbitrary data.)
我々は、メモリの一部を解放するときも同様に、実行時 ​​にそれを再割り当てし、任意のデータを上書きする許可を与えます。
[](The rest of our program should not depend on what happens later with the deallocated memory -- provided it was really free, not needed further in the program.)
我々のプログラムの残りの部分は後に何が起こるかに依存する必要のない解放されたメモリです - プログラムの中で、さらに必要とされない、本当に解放されているメモリです。
[](In fact, one may define 'memory not in use' as arbitrary non-deterministic changes to that memory not affecting the rest of the program.)
実際に、そのメモリは、プログラムの他の部分に影響を与えないように、任意の非決定的な変更として「メモリ使用していない」とされるかもしれません。
[](Deallocating memory still in use will affect the rest of the program -- often, crash it.)
まだ使用中のメモリの割り当てを解除すると、プログラムの他の部分に影響を与えます - 多くの場合、クラッシュします。
[](Incidentally, unsound types inferred for our examples often lead to the same result.)
そして、我々の例では多くの場合同じ結果につながるため不完全な型が推論されます。

#### References
#### 参考資料

[unsound.ml](http://okmij.org/ftp/ML/generalization/unsound.ml) [9K]

[](Complete code for the toy type inferencer with the unsound generalization, with many more examples of unsound inference)
不完全な一般化とおもちゃの型推論の完全なコードと不完全な推論のより多くの例
