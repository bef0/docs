# 4.レベルによる効率的な一般化

[](## 4.Efficient generalization with levels)

1.

[](This section continues the exposition of the ideas behind Rémy's algorithm.)
このセクションでは、レミーのアルゴリズムの背後にあるアイデアの博覧会を続けています。
[](Now that we have seen how the unsound generalization relates to the releasing of memory still in use, we apply the standard fix for premature deallocation -- ownership tracking, or regions -- and cure the unsound generalization without much overhead.)
今、我々は不完全な一般化のまだ使用中のメモリの解放に関連する問題を見てきたので、早すぎる解除（所有権の追跡、またはリージョン）のための標準的な修正プログラムを適用し、多くのオーバーヘッドなしで不完全な一般化を直します。
[](We develop two algorithms.)
我々は2つのアルゴリズムを開発します。
[](The simpler one, sound_eager, is motivated and explained in this section.)
１つはシンプルなsound_eagerで、このセクションで動機付けを与え、そして説明します。
[](The optimal sound_lazy, which captures the main features of the Rémy algorithm, is presented next.)
次のセクションでは、最適なsound_lazyレミアルゴリズムの主な特徴を捉え、提示します。

[](Clearly, before deallocating memory we must check if it is still in use.)
明らかに、メモリの割り当てを解放する前に、まだ使用されているかを我々は確認する必要があります。
[](Naively, we could scan all memory known to be in use looking for references to the deallocation candidate -- in other words, do the full garbage-collection marking pass and see if our piece of memory got marked.)
単純に、我々は解放候補への参照を探して使用中であることが知られているすべてのメモリをスキャンできます - つまり、パスをマークする完全なガベージコレクションを行い、メモリの我々のピースがマークされてしまったかどうかを確認します。
[](Put it this way, the check seems awfully expensive.)
このようにそれを導入して、チェックする事は非常にコストがかかりそうです。
[](At least we should wait until garbage accumulates, to collect it en masse.)
ゴミが蓄積されるまで少なくとも我々は一斉にそれを収集するために、待つ必要があります。
[](Alas, in the Hindley-Milner type system we cannot delay quantification arbitrarily, since the generalized type can be used right away.)
悲しいかな、Hindley-Milner型システムでは、一般化した型をすぐに使用することができるため、我々は、任意に定量化​​を遅延させることができません。

[](More promising is ownership tracking, associating an allocated resource with an owner, an object or a function activation.)
所有者によって割り当てられたリソースの関連付けられた、オブジェクトまたは関数の活性化の所有権の追跡がより有望です。
[](Only the owner may deallocate its resources.)
唯一の所有者は、そのリソースの割り当てを解放する必要があります。
[](A similar strategy is regions, which are areas of memory for heap-allocated data created by a lexically-scoped let region primitive.)
同様の戦略は、レキシカルスコープletリージョンプリミティブによって作成されたヒープに割り当てられたデータのためのメモリ領域であるリージョンです。
[](When letregion goes out of scope, its whole whole region is summarily deallocated.)
letリージョンがスコープの外に出ると、その全体、リージョン全体が即決割当て解除されます。
[](This idea matches the generalization well.)
この考えはうまく一般化とマッチします。
[](In the Hindley-Milner system, generalization is always a part of let.)
Hindley-Milnerシステムでは、一般化は常にletの一部です。
[](A let-expression let x = e in e2 is the natural owner of all type variables allocated when inferring the type of e.)
let式 let x = e in e2は、eの型を推論する際に割り当てられたすべての型変数の自然な所有者です。
[](When the type of e is found, all free type variables still owned by the let-expression can be disposed of, that is, quantified.)
eの型が発見されたとき、let式が所有するすべての自由型変数を解放することができる、すなわち、定量化されます。

---------------

2.

[](These intuitions underlie the sound and efficient generalization algorithms.)
これらの直感は、健全かつ効率的な一般化アルゴリズムの基礎となります。
[](The first is sound_eager, described in the rest of the section.)
最初はsound_eagerを残りのセクションで説明します。
[](Its code differs only in small, but significant, details from the toy Hindley-Milner inferencer from the previous section.)
前のセクションのおもちゃのHindley-Milner型推論とsound_eggerのコードには、細部に小さくて重要な違いがあります。
[](We will only explain these small differences; the complete code is available below.)
我々は、これらの小さな違いを説明します;完全なコードは以下に提供されています。
[](The main difference is that free type variables, albeit unbound, are now owned, and contain the `pointer' to their owner.)
主な違いは、自由型変数はバインドされませんが、その所有者に'ポインタ'が今所有し含まれるということです。
[](The owner, always a let expression, is identified by a positive integer called level.)
常に所有者のlet式は、正の整数の呼び出しレベルで識別されます。
[](This level is essentially the de Bruijn level, only referring to the owner rather than the binder.)
このレベルは本質的にはde Bruijnレベルで、所有者だけではなくバインダーも参照します。
[](That is, the level is the nesting depth of the owning let expression in the abstract syntax tree [AST]; level 1 corresponds to the [implicit] top-level let.)
つまり、レベルは抽象構文木(AST)内の所有するlet式のネストの深さです;レベル1は（暗黙の）トップレベルのletに対応しています。
[](Incidentally, although both lets in [let x = e1 in eb1, let y = e2 in eb2] have level 2, no confusion can arise as neither let is in each other scope and hence their regions are disjoint.)
(ちなみに、(let x = e1 in eb1, let y = e2 in eb2)の両方がレベル2を持っていますが、どちらのletも互いにスコープ内にあるので、それぞれのリージョンが互いに素であるようにして、混乱が生じないようにできます。)
[](Since the depth of typeof recursion equals the AST depth of the expression it type checks, the level is equal to the let-expression's type checking recursion depth. )
typeofの再帰の深さは、型チェック中の式のASTの深さに等しいので、レベルはlet式の型チェックの再帰の深さと同じです。
[](That depth is simple to determine, with the help of one reference cell.)
一つの参照セルの助けを借りれば、その深さを判断するのは簡単です。

```
type level = int
let current_level = ref 1
let enter_level () = incr current_level
let leave_level () = decr current_level
```

[](The type inferencer maintains the let type-checking depth:)
型推論器はlet型チェック深度を正しく保持します：

```
let rec typeof : env -> exp -> typ = fun env -> function
  ... (* the other cases are the same as before *)
      (*その他の場合は、以前と同じです *)
  | Let (x,e,e2) ->
      enter_level ();
      let ty_e = typeof env e in
      leave_level ();
      typeof ((x,gen ty_e)::env) e2
```

[](The only change to the main type-inference function was adding enter_level and leave_level to track the level.)
メインの型推論関数の変更は、enter_levelとleave_levelをレベルを追跡するために追加するだけです。
[](The rest of typeof is literally the same as in the original toy version.)
残りのtypeofは文字通り、オリジナルのtoyのバージョンと同じです。

[](Free type variables now carry the level identifying their owner.)
自由型変数は現在、彼らの所有者を特定するレベルを持ち回ります。
[](A freshly allocated type variable receives the current_level, meaning that its owner is the latest let being type-checked.)
新しく割り当てられた型変数がcurrent_levelを受け取るということは、その所有者が最新のものであることを意味するので、型チェックされます。
[](In region-based memory management, all new memory is allocated in the innermost alive region.)
(リージョンベースメモリ管理では、すべての新しいメモリは、最も内側の生きているリージョンで割り当てられます。)

```
type typ =
  | TVar of tv ref               (* type (schematic) variable タイプスキーム変数 *)
  | QVar of qname                (* quantified type variable 定量化型変数 *)
  | TArrow of typ * typ
and tv = Unbound of string * level | Link of typ

let newvar : unit -> typ =
  fun () -> TVar (ref (Unbound (gensym (),!current_level)))
```

---------------

3.

[](Just as an assignment may change the owner of an allocated memory, unification may change the level of a free type variable.)
代入が割り当てられたメモリの所有者を変更することがあるように、単一化は自由型変数のレベルを変更することがあります。
[](For example, if ty_x and ty_y are both free, ty_y has level 2, ty_x has level 1, and ty_x is unified with the type TArrow(ty_y,ty_y), the arrow type and its components are exported into region 1, and so the level of ty_y is changed to 1.)
たとえば、ty_xとty_yがともに自由で、ty_yはレベル2、ty_xはレベル1で、ty_xが型TArrow(ty_y,ty_y)と単一化する場合、関数の型とその構成要素は、リージョン1にエクスポートされてty_yのレベルを1に変更します。
[](One may view the above unification as replacing all occurrences of ty_x with TArrow(ty_y,ty_y).)
TArrow(ty_y,ty_y)でty_xのすべての出現を置換する事と上記の単一化をみなす事が出来ます。
[](Some occurrences may be outside the scope of the inner, level-2 let.)
いくつかの出現は、内側のレベル2のletの範囲外にあるかもしれません。
[](Therefore, when the type checking of that inner let finishes, ty_y should not be deallocated.)
そのため、その内側のletの型チェックが終了しても、ty_yは割り当てを解除すべきではありません。
[](With the updated ty_y level, it won't be.)
更新されたty_yレベルでは、解放できないのです。
[](Thus unifying a free type variable ty_x with t has to update the levels of each free type variable ty_y in t to the smallest of ty_y and ty_x levels.)
このように自由変数ty_xとtの単一化は、ty_yとty_xの最小レベルのt内でそれぞれの自由変数ty_yのレベルを更新することがあります。
[](Unifying a free type variable with t also has to do the occurs check, which too traverses the type.)
自由型変数tを単一化する際も出現チェックに関係しており、型をトラバースします。
[](The two traversals can be merged.)
2つのトラバースはマージすることができます。
[](The new occurs does the occurs check and updates the levels:)
新しいoccurs関数は出現チェックとレベルの更新を行います:

```
let rec occurs : tv ref -> typ -> unit = fun tvr -> function
  | TVar tvr' when tvr == tvr' -> failwith "occurs check"
  | TVar ({contents = Unbound (name,l')} as tv) ->
     let min_level =
       (match !tvr with Unbound (_,l) -> min l l' | _ -> l') in
     tv := Unbound (name,min_level)
  | TVar {contents = Link ty} -> occurs tvr ty
  | TArrow (t1,t2)            -> occurs tvr t1; occurs tvr t2
  | _ -> ()
```

[](The only difference from the original occurs code is the second clause in the pattern-match.)
パターンマッチの２つめの１カ所がオリジナルのoccursコードとの違います。
[](The unification code does not have to be modified at all.)
単一化のコードは全く変更する必要はありません。
[](Finally, we fix the generalization function, to make it sound:)
最後に、我々はそれが完璧に動くように、一般化関数を修正します:

```
let rec gen : typ -> typ = function
  | TVar {contents = Unbound (name,l)}
     when l > !current_level -> QVar name
  | TVar {contents = Link ty} -> gen ty
  | TArrow (ty1,ty2) -> TArrow (gen ty1, gen ty2)
  | ty -> ty
```

---------------

4.

[](The change is minimal: the condition when l > !current_level.)
変更は以下の条件を加えるだけです:  when l > !current_level。
[](Recall the new typeof code:)
新しいtypeofのコード:

```
let rec typeof : env -> exp -> typ = fun env -> function
  ... (* その他の場合は、以前と同じです *)
  | Let (x,e,e2) ->
    enter_level ();
    let ty_e = typeof env e in
    leave_level ();
    typeof ((x,gen ty_e)::env) e2
```

[](It invokes gen after the region established for type checking e exits.)
リージョンが確立された後にeの型チェックを終了するためgenを呼び出します。
[](A free type variable still owned by that region will have the level greater than the current.)
まだそのリージョンが所有する自由型変数は、現在よりも高いレベルを持つことになります。
[](Since the region is now dead, any such type variable may be deallocated, that is, quantified.)
リージョンは、今、死んだのでどのような型変数でも割り当てを解除出来る、つまり、定量化されます。

---------------

5.

[](These are all the changes of sound_eager from the unsound toy algorithm, which fix the type inference.)
これらが不完全なおもちゃのアルゴリズムからsound_eagerへの型推論を修正するすべての変更です。
[](Here is the old problematic example)
ここでは、古い問題の例を次に示します。

```
fun x -> let y = x in y
```

[](Diagramming the TVar operations shows no problems now:)
TVarの操作の図は、今は問題がないことを表しています:

```
1  1   ty_x/1 = newvar ()          (* fun x -> ...                 *)
2  2     ty_e =                    (* let y = x in y               *)
3  2        inst ty_x/1            (* ty_xと同じxのために推論        *)
2  1     ty_y = gen ty_e           (* ty_x/1 は自由だが, しかし   *)
                                   (* level = current, 定量化出来ず  *)
                                   (* 解放出来ません                 *)
1  1   TArrow(ty_x/1, inst ty_y)   (* inferred for: fun x -> ...   *)
```

[](The first column of numbers shows the typeof recursion depth, or the depth of the AST node being type-checked.)
最初の列の数はtypeofの再帰の深さでありASTノードが型チェックを始めた深さを示します。
[](The number in the second column is the current_level, the let nesting depth.)
2列目の数はcurrent_levelで、letのネストの深さです。
[](We write the level of a free type variable after the slash, as in ty_x/1.)
我々はty_x/1のようにスラッシュの後に自由型変数のレベルを記述します。
[](That variable is no longer quantified by gen at depth 2 (level 1) since ty_x/1 belongs to to the current, still active region 1.)
深さ2（レベル1）ty_x/1で、現在、まだアクティブ領域1に属するのでその変数はもうgenによって定量化されることはありません。
[](Therefore, the inferred type is 'a->'a, as expected.)
そのため、期待どおりに推論された型は'a->'aです。

-----------

[](In a slightly more complex example,)
もう少し複雑な例だと、

```
fun x ->
  let y = fun z ->
    x
  in
  y
```

[](the type variable ty_x for the type of x is allocated at level 1, whereas ty_z is allocated at level 2.)
xの型である型変数ty_xにはレベル1、ty_zにはレベル2に割り当てられています。
[](After the inner let, region 2, is finished, ty_z/2 will be quantified and disposed of, but ty_x/1 will not.)
内側のletの後ろでリージョン2は終了し, ty_z/2が定量化され解放されますが、ty_x/1は解放されません。
[](The inferred type therefore is 'a->'b->'a.)
そのため推論された型は'a->'b->'aです。
[](The reader is encouraged to diagram other examples, to check that the inferred types are sound.)
読者には、推論された型が健全であることを確認するために、他の例を図式化することをお勧めします。

----------

[](Level tracking may look like reference counting.)
レベルの追跡は、参照カウントに似たように見えたり見えなかったりします。
[](However, rather than counting the number of users for a free type variable, we keep track of only one user, the one with the widest scope.)
けれども、自由型変数のユーザー数を数えるのではなく、むしろ、1つのユーザーが１つの広い範囲を追跡するのです。
[](Level tracking does look a lot like generational garbage collection: Memory is allocated in the young generation, and summarily disposed of at the minor (youngest) collection, unless it is 're-parented' or referenced from the stack.)
レベルの追跡は、世代別ガベージコレクションのように多くのことを見ていません：
メモリは、若い世代に割り当てられ、即座にマイナー（最も若い）コレクションで解放され、そうでなければ、再ペアレント化されるかまたはスタックから参照されます。
[](The old generation does not have to be scanned for references to the new generation, since no such references are expected -- unless there was an assignment of a young value to the field of an old data structure.)
古い世代はそのような参照が予想されないので、新しい世代への参照のためにスキャンする必要はありません - 古いデータ構造体のフィールドに若い値の割り当てがあった場合を除きますが。
[](A generational garbage collector (such OCaml GC) keeps track of inter-generational assignments.)
世代別ガベージコレクタ（そのようなOCamlのGCは）は世代間の割り当てを追跡します。
[](At the minor collection, young data referred from the old are promoted to the old generation.)
マイナーコレクションでは、古いデータから参照されている若いデータは旧世代に昇格します。
[](Type generalization indeed looks very similar to the minor GC collection.)
確かに型の一般化はマイナーGCと非常によく似ているのです。

#### References
#### 参考資料

[sound_eager.ml](http://okmij.org/ftp/ML/generalization/sound_eager.ml) [10K]

[](Complete code for the toy type inferencer with the sound_eager generalization, with many more examples of now sound inference)
sound_eagerの一般化とおもちゃの型推論器の動作するコードと今の完璧な推論の沢山のサンプル
