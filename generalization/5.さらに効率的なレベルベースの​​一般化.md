# 5.さらに効率的なレベルベースの​​一般化

[](## 5. Even more efficient level-based generalization)

1.

[](This section continues the exposition of the ideas behind Rémy's algorithm and presents sound_lazy, an optimized version of sound_eager from the previous section.)
このセクションでは、レミーのアルゴリズムの背後にあるアイデアの博覧会を継続し、前セクションのsound_eagerの最適化バージョンであるsound_lazyを提供します。
[](The sound_lazy algorithm eschews repeated, unnecessary traversals of the type during unification, generalization and instantiation, and avoids copying the parts that do not contain variables to generalize or instantiate, thus improving sharing.)
sound_lazyアルゴリズムは単一化時の型の不必要なトラバース、一般化とインスタンス化の繰り返しを避けて、さらに一般化またはインスタンス化する変数が含まれていない部分をコピーして回避することで共有を改善します。
[](The algorithm delays the occurs check and the level updates, so that the unification with a free type variable takes constant time.)
自由型変数の単一化は一定の時間がかかるので、このアルゴリズムは、出現チェックとレベルの更新を遅延します。
[](Levels are updated incrementally and on demand.)
レベルはオンデマンドでインクリメンタルに更新されます。
[](All in all, sound_lazy embodies the main ideas of Rémy's algorithm.)
全体的に、sound_lazyはレミーのアルゴリズムの主要なアイデアを具体化します。
[](Some of these ideas are implemented in the OCaml type checker.)
これらのアイデアのいくつかは、OCamlの型検査器に実装されています。

[](To carry on the optimizations, we change the syntax of types.)
最適化を実行するために、我々は型の構文を変更します。
[](Recall that in sound_eager, types were comprised of free or bound type variables TVar, (implicitly universally) quantified type variables QVar and function types TArrow.)
sound_eagerでは、型は自由変数や型変数の束縛TVar、（暗黙的に普遍的に）定量化された型変数のQVar、関数型TArrowで構成されていました。
[](The first, seemingly unprincipled change, is to eliminate QVar as a distinct alternative and dedicate a very large positive integer -- which should be treated as the inaccessible ordinal ω -- as a generic_level.)
まず、一見無節操な変更は、明確な代替手段としてQVarを排除し、- アクセス不可能な序数ωとして扱われるべき - generic_levelとして非常に大きな正の整数を専用に使うようにすることです。
[](A free type variable TVar at generic_level is taken to be a quantified type variable.)
generic_levelの自由型変数TVarを定量化された型変数とみなします。
[](More substantially, all types, not only free type variables, have levels now.)
より実質的に、自由型変数だけではなく、すべての型が、今のレベルを持ちます。
[](The level of a composite type (TArrow in our case) is an upper, not necessarily exact, bound on the levels of its components.)
複合型（我々の場合TArrow）のレベルは、必ずしも正確にそのコンポーネントのレベルにバインドされているとは限りません。
[](In other words, if a type belongs to an alive region, all its components should be alive.)
言い換えれば、型が生きているリージョンに属している場合、そのすべてのコンポーネントが生きている必要があります。
[](It immediately follows that if a (composite) type is at generic_level, it may contain quantified type variables.)
それはすぐに（複合）型がgeneric_levelであれば、それは定量化された型変数を含むことができることになります。
[](Contrapositively, if a type is not at generic_level, it does not contain any quantified variable.)
対して、型がgeneric_levelでない場合は、任意の定量化変数が含まれていません。
[](Therefore, instantiating such a type should return the type as it is without traversing it.)
そのため、このような型のインスタンスを作成しトラバースせずにそのまま型を返す必要があります。
[](Likewise, if the level of a type is greater than the current level, it may contain free type variables to generalize.)
同様に、型レベルが現在のレベルよりも大きい場合も、一般化される自由型変数を含むことができます。
[](On the other hand, the generalization function should not even bother traversing a type whose level is equal or less than the current.)
一般化関数でも、そのレベルが等しいか、または現在よりも少ない型をわざわざトラバースする必要はありません。
[](This is the first example of how levels help eliminate traversals and rebuildings of a type, improving sharing.)
これはレベルが共有を改善し、型のトラバースと再生成をなくすために、どのように役立つかの最初の例です。

[](Unifying a type t with a free type variable should update t's level to the level of the type variable if the latter level is smaller.)
後者のレベルが小さい場合は自由型変数と型tを単一化すると、型変数のレベルにtのレベルを更新する必要があります。
[](For a composite type, such an update means recursively updating the levels of all components of the type.)
複合型の場合は、このような更新は、再帰型のすべてのコンポーネントのレベルを更新することを意味します。
[](To postpone costly traversals, we give composite types two levels: level_old is an upper bound on the levels of type's components; level_new, which is less or equal to level_old, is the level the type should have after the update.)
コストがかかるトラバースを延期するために、我々は複合型に2レベルを与えます：level_oldは型のコンポーネントのレベルの上限です。
level_newはlevel_oldより小さいか等しい場合に更新後に持つべき型のレベルです。
[](If level_new < level_old, the type has pending level updates.)
level_new < level_oldなら型はレベルの更新を保留します。
[](The syntax of types in sound_eager is thus)
従ってsound_eagerの型の構文は次のとおりです。


```
type level = int
let generic_level = 100000000           (* OCaml の typing/btype.ml のようにする *)
let marked_level  = -1                  (* for marking a node, to check*)
                                        (* for cycles                  *)
                                        (* ノードをマークするための   *）
                                        (* サイクルのチェック *）

type typ =
  | TVar of tv ref
  | TArrow of typ * typ * levels
and tv = Unbound of string * level | Link of typ
and levels = {mutable level_old : level; mutable level_new : level}
```

[](We have not explained marked_level.)
我々はmarked_levelを説明していません。
[](The occurs check on each unification with a free type variable is expensive, raising the algorithmic complexity of the unification and type checking.)
自由型変数との単一化でそれぞれに出現チェックを行う事は高価であり、単一化と​​型チェックのアルゴリズムを複雑度を上げます。
[](We now postpone this check, until the whole expression is type checked.)
式全体の型チェック終わるまで、我々はこのチェックを延期します。
[](In the meanwhile, unification may create cycles in types.)
一方で、単一化によって型の循環を作成するかもしれません。
[](Type traversals have to check for cycles, or risk divergence.)
循環を回避し、リスクを避ける為に型をトラバースしてチェックする必要があります。
[](The marked_level is assigned temporarily to level_new of a composite type to indicate the type is being traversed.)
marked_levelは型がトラバースされていることを示すために複合型のlevel_newに一時的に割り当てられています。
[](Encountering marked_level during a traversal means detecting a cycle, which raises the occurs check error.)
全探索時にmarked_levelに遭遇すると、出現チェックエラーを発生させ、サイクルを検出したことを意味します。
[](Incidentally, in OCaml types are generally cyclic: (equi-)recursive types arise when type checking objects and polymorphic variants, and when -recursive flag is in effect.)
ところで、OCamlの型は一般的に環状であり:オブジェクトと多相型バリアントを型チェック時に再帰的に等しい型が生じ、このとき再帰フラグが有効になっています。
[](The OCaml type checker uses a similar marked-level trick to detect cycles and avoid divergence.)
OCamlの型検査は、サイクルを検出して発散を避けるために、同様のマークされたレベルのトリックを使用しています。

--------------

2.

[](The sound_lazy unification has several important differences from sound_eager:)
sound_lazyの単一化はsound_eagerからいくつかの重要な相違点があります:

```
let rec unify : typ -> typ -> unit = fun t1 t2 ->
  if t1 == t2 then ()                   (* t1 and t2 are physically the same *)
  else match (repr t1,repr t2) with
  | (TVar ({contents = Unbound (_,l1)} as tv1) as t1,      (* unify two free vars *)
    (TVar ({contents = Unbound (_,l2)} as tv2) as t2)) ->
      if l1 > l2 then tv1 := Link t2 else tv2 := Link t1  (* bind the higher-level var *)
  | (TVar ({contents = Unbound (_,l)} as tv),t')
  | (t',TVar ({contents = Unbound (_,l)} as tv)) ->
      update_level l t';
      tv := Link t'
  | (TArrow (tyl1,tyl2,ll), TArrow (tyr1,tyr2,lr)) ->
      if ll.level_new = marked_level || lr.level_new = marked_level then
        failwith "cycle: occurs check";
      let min_level = min ll.level_new lr.level_new in
      ll.level_new <- marked_level; lr.level_new <- marked_level;
      unify_lev min_level tyl1 tyr1;
      unify_lev min_level tyl2 tyr2;
      ll.level_new <- min_level; lr.level_new <- min_level
  (* everything else is the unification error *)

and unify_lev l ty1 ty2 =
  let ty1 = repr ty1 in
  update_level l ty1;
  unify ty1 ty2
```

[](where the auxiliary repr, like OCaml's Btype.repr, chases links of bound variables returning a free variable or a constructed type.)
補助関数のreprでは、OCamlのBtype.reprのように、自由変数または構築された型を返すバインド変数のリンクを追います。
[](Unlike OCaml, we do path compression.)
OCamlとは異なり、我々は、パスの圧縮を行います。
[](The unification function no longer does the occurs check;)
単一化関数は、もはや出現チェックを行いません。
[](therefore, it has to make an effort to detect accidentally created cycles.)
そのため、誤って作成されたサイクルを検出する努力をしなければなりません。
[](Unifying with a free variable now takes constant time, to bind the variable after a shallow update_level.)
今一定の時間がかかる自由変数のための単一化は、浅いupdate_levelした後の変数をバインドします。

[](The function update_level is one of the key parts of the optimized algorithm.)
update_level関数は最適化されたアルゴリズムの重要な部分の一つです。
[](Often, it merely promises to update the level of a type to the given level.)
多くの場合、それは指定されたレベルまでの型のレベルを更新することの約束にすぎません。
[](It works in constant time and maintains the invariant that a type level may only decrease.)
これは、一定の時間で動作し、型レベルだけ減少することがあることを不変に維持します。
[](The level of a type variable is updated immediately.)
型変数のレベルはすぐに更新されます。
[](For a composite type, level_new is set to the desired new level if the latter is smaller.)
複合型の場合は、後者の方が小さい場合、level_newを所望の新たなレベルに設定します。
[](In addition, if previously level_new and level_old were the same, the type is put into the to_be_level_adjusted queue for later update of the levels of the components.)
また、以前level_newとlevel_oldは同じであった場合、コンポーネントのレベルの後の更新のために、型はto_be_level_adjustedキューに入れられます。
[](This work queue is akin to the list of assignments from the old generation to the young maintained by a generational garbage collector [such as the one in OCaml].)
この作業キューは、(このようなOCamlの中の一つとして)世代別ガベージコレクタによって維持される古い世代から若いへの割り当てのリストに似ています。
[](Incidentally, a unification of two TArrow types has to traverse the types anyway, and so it does pending level updates along the way.)
ちなみに、二つの単一化TArrow型はとにかく型をトラバースしなければならないので、途中でレベルの更新を保留しません。


```
let to_be_level_adjusted = ref []

let update_level : level -> typ -> unit = fun l -> function
  | TVar ({contents = Unbound (n,l')} as tvr) ->
    assert (not (l' = generic_level));
    if l < l' then
      tvr := Unbound (n,l)
  | TArrow (_,_,ls) as ty ->
    assert (not (ls.level_new = generic_level));
    if ls.level_new = marked_level then failwith "occurs check";
    if l < ls.level_new then begin
      if ls.level_new = ls.level_old then
        to_be_level_adjusted := ty :: !to_be_level_adjusted;
      ls.level_new <- l
    end
  | _ -> assert false
```

--------------

3.

[](Pending level updates must be performed before generalization: After all, a pending update may decrease the level of a type variable, promoting it to a wider region, and hence save the variable from quantification.)
保留中のレベルの更新は、一般化する前に実行する必要があります:
結局、保留中の更新がより広い領域にそれを広め、型変数のレベルを減少させることがあるため、定量化から変数を保存します。
[](Not all pending updates have to be forced however -- only of those types whose level_old > current_level.)
すべての保留中の更新を強制する必要はありません - ただしlevel_old > current_levelを持つそれらの型のみ強制されます。
[](Otherwise, a type contains no variables generalizable at the present point, and the level update may be delayed further.)
それ以外の場合は、型は現時点での一般化なしの変数が含まれておらず、レベルの更新は、さらに遅れる場合があります。

[](The generalization function searches for free TVars that belong to a dead region [that is, whose level is greater than the current] and sets their level to generic_level, hence quantifying the variables.)
一般化関数は死んでいるリージョンに属する（つまり、そのレベルが現在よりも大きい）自由TVarsを検索し、彼らのレベルをgeneric_levelに設定することで、変数を定量化します。
[](The function traverses only those parts of the type that may contain type variables to generalize.)
この関数は一般化する型変数を含むことができる型の部分のみトラバースします。
[](If a type has the [new] level of current_level or smaller, all its components belong to live regions and hence the type has nothing to generalize.)
型はcurrent_level以下の（新しい）レベルであれば、すべてのコンポーネントは、生きているリージョンに属し、それゆえ型は一般化に関係しません。
[](After the generalization, a composite type receives generic_level if it contains a quantified type variable.)
一般化した後、定量化された型変数が含まれている場合に複合型はgeneric_levelを受け取ります。
[](Later on, the instantiation function will, therefore, only look through those parts of the type whose level is generic_level.)
その後、インスタンス生成関数は、generic_levelのレベルである型の部分にのみ目を通します。

```
let gen : typ -> unit = fun ty ->
  force_delayed_adjustments ();
  let rec loop ty =
    match repr ty with
    | TVar ({contents = Unbound (name,l)} as tvr)
      when l > !current_level -> tvr := Unbound (name,generic_level)
    | TArrow (ty1,ty2,ls) when ls.level_new > !current_level ->
      let ty1 = repr ty1 and ty2 = repr ty2 in
      loop ty1; loop ty2;
      let l = max (get_level ty1) (get_level ty2) in
      ls.level_old <- l; ls.level_new <- l   (* set the exact level upper bound *)
              (*上限正確なレベルを設定します*)
    | _ -> ()
in loop ty
```

[](The type checker typeof remains the same, entering a new region when type checking a let expression.)
型チェッカーのtypeofはlet式を型チェックするときにそのまま新しいリージョンに入ります。
[](Please see the source code for details.)
詳細については、ソースコードを参照してください。

[](We have presented the optimized sound_lazy type generalization algorithm that not only avoids scanning the whole type environment on each generalization.)
我々は、各一般化で全体のタイプの環境をスキャンして回避しただけではない最適化されたsound_lazy型一般化アルゴリズムを提示しました。
[](The algorithm also avoids the occurs check on each unification with a free type variable, so that this unification takes constant time.)
このアルゴリズムは、また一定の時間がかかるような自由型変数の単一化で、各単一化ごとの出現チェックを避けることができます。
[](The algorithm eliminates unnecessary type traversals and copying, saving time and memory.)
このアルゴリズムは、時間とメモリを節約し、不要な型トラバーサルとコピーを排除します。
[](Two ideas underlie the optimizations, besides the type levels for free type variables.)
二つのアイデアは自由型変数の型レベルに加えて、最適化の基礎となります。
[](First is the assigning of levels to composite types, to give us an idea what a type may contain without looking though it.)
一つ目は複合型のレベル割り当てをすることで、どのような型が含まれているかを見なくてもよくするアイディアです。
[](The second principle is delaying expensive actions [type traversals] with the hope they will get done in the future alongside of something else.)
第二の原理は、それらが他の何かと一緒に将来的にこなすことを期待して、コストの高いアクション（型トラバーサル）を遅延させています。
[](In other words, if dealing with a problem is postponed long enough, it may go away:)
言い換えれば、問題に対処するのに十分に遅延された場合、それは必要なくなるかもしれないのです:
[](procrastination sometimes helps.)
遅延評価戦略は、しばしば役立つのです。

#### References
#### 参考資料

[sound_lazy.ml](http://okmij.org/ftp/ML/generalization/sound_lazy.ml) [18K]

[](Complete code for the optimized toy type inferencer, again with many examples)
最適化されたオモチャの型推論の動作するコードと、また沢山の例
